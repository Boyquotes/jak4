:shop_entry
{talked == 0} Ah, the tourist has found fit to visit me!
	* The shop smells of burning incense. It's different from home. A stronger, more bitter smell.
	[You] Hi, I'm Jackie!
		It's nice to meet you!
	Welcome to my shop, Jackie! Whatever brings you so far from your home, to our modest city?
	> I'm here to see the Medium.
		{mention("medium")}... Ah.
		{format("italic")} Braalni Mhote.
		{animation("show_medium")} "The Medium of Bralna". I do not believe the machine has worked in some years.
		[You] Yeah. I talked with the village elder.
		So he has roped you into repairing that contraption?
		[You] I'm glad to help! Especially since I need to see it.
		So you do...
		But let's put aside such matters. I doubt you came in here to have me talk your ears off. You probably came here to buy something.
	> My mom used to live around here...
		And from the tone in your voice...
		I presume she is not with us.
		[You] That's... yeah.
			She's not.
		I am sorry to hear that. The loss of a parent is deep, though better than the alternative. You are here for unsettled affairs, or memories?
		[You] Uh, both, I guess...
		> I'm here to see the Medium.
			{mention("medium")} Oh.
			And you wish to commune with her.
			{animation("show_medium")} Quite frankly, I do not think the Medium is such a useful machine.
			It does not talk to souls. Only simulacra. Faint reflections of those lost.
			[You] Yeah. I know how it works. It's just... important to me.
			I hope you find what you are looking for. I hope that machine can help you.
			If you need anything during your stay, I probably have it in my shop.
		> {skip()} (Say nothing more)
			I see. Well, if you need anything for your travels, perhaps I have it in my shop.
	> {skip()} (Come up with a clever excuse)
		[You] I, uh... um, I
		There is no need to tell me if you are shy.  It must be important to come all this way.
		Besides, you probably came in here to buy something, not to have me talk your ears off.

{otherwise} Welcome back, Jackie!
	* {talked > 10} The familiar smell of the incense comforts you.
	* {otherwise if talked > 7} The burning incense is not unpleasant.
	* {otherwise if talked > 4} The incense is strong.
	* {otherwise} The bitter incense burns at your sinuses.
	
	{Global.stat("left_village")} I am glad you have returned safely.
	Do let me know if you need anything.

:shop_replies
{animation("reset")} 
> Sure, I'll buy some stuff.
	With pleasure, Jackie!
	{event("open_shop")}
	{exiting()} [You] Thanks!
	I hope you found what you were looking for...
> What do you sell?
	Elixirs! Deathgnats! Flags!
	> What do the elixirs do?
		Many things.
		Increase your stamina, strength, speed.
		Some temporarily, others permanently.
	> {format("italic")} Deathgnats?
		{animation("show_deathgnat")}A nasty pest, but not nearly as harmful as its larger cousins.
		We catch them to use as a second currency. Typically for larger purchases, such as capacitors.
		{animation("reset")}
		{Global.stat('bug') == 0} You do not have any gnats, but I'm sure you shall find some.
		{otherwise if Global.stat('bug') == 1} I see you have found one! Nasty creatures, aren't they?
			[You] It keeps vibrating. People carry these around?
			Hahaha!
			You do not need to keep it alive. It would be quite unbearable keeping dozens of live deathgnats.
			{animation("show_deathgnat")} The one I keep is quite a handful.
			{animation("reset")}
		{otherwise} You have #{Global.count('bug')} gnats on your person, and have found a total of #{Global.stat('bug')}.
			Commendable work!
		> Where do I find deathgnats?
			Ha! Where can you not find them? They are pervasive!
			Sun or shade, sand or snow, they love to flutter about, biting at any who are not careful.
		> ... What are the capacitors for?
			Many ancient machines, doors and elevators and such, need them for power.
			{mentioned("medium")} I have heard the Medium requires three capacitors.
			There are not many left in this canyon, though some traders will have one or two for sale.
			So would you like to buy my gnat?
		> Okay, I think I understand.
			Immaculate! Let me know if you want one. Once it is gone, it is gone!
	> What are the flags for?
		They mark where you have been, a reminder, I suppose.
		Adventurers seem keen to stock up on them.
		* [Hint: Flags serve as consumable checkpoints. When you place one, you will teleport back to it upon death.]
	> Do you sell any coats? So many people are wearing lovely fabrics.
		They are not usually sold... We get our clothes from hunting alpha specters. It is too dangerous to hunt for profit.
		You may be able to trade with people. They won't mind losing an outfit if they get another.
		* [Hint: You can trade clothes with nearly any NPC by pressing "C" on the keyboard, or Button 3 (the top face button)on a gamepad. This works both in and out of dialog].
	> Ah okay, that makes sense.
		It does!
	{goto("shop_replies")}
> I'll see you around!
	{exiting()}
	Take care, Jackie...
{exit()}

:_exit
[You] Gotta go, see you!
{exit()}

:_coat
{traded_coats()} [You] Could we trade outfits again?
	Hm, you changed your mind?
	How about the same deal as last time: my coat, in exchange for yours plus fifty gems.
{otherwise}
	[You] Would you be willing to trade outfits? Yours is so lovely...
	Hm... I only have two sets.
	If you give me your coat in exchange, plus... oh, fifty gems? Then we have a deal.
> I'll think about it and ask again later.
> {Global.count("gem") > 50} Deal. My coat and fifty gems.
	{Global.add_item("gem", -50)} Very well.
	{swap_coats} I hope you enjoy.
> {otherwise} Ouch. I don't have fifty gems.
	Well, I'm sure you can find some if you are interested.
Anything else I can help you with?
{goto("shop_replies")}