:_entry126
// Riley's introduction
{main_speaker.saved}
	+{riley/126/talked}
	{talked == 0}
		Eey! Klerdena sur!
		[You]  Uh, apologies.  That's "thank you" in Yaqazi, correct? I forgot how to say "you're welcome".
		Wait, you're a Vaeler? Wow! Well, thanks again! You really saved me there!
		{remember("She was quite grateful when I helped her off that scrap pile.")}
		[You] I did?
		Yeah, I was pinned down by those deathgnats, but you took them out no problem! I'm Riley, by the way.
		[You] Jackie. It was no trouble. One of them had a lovely coat.
		You're some kind of hunter?
		[You] Oh no, nothing like that. I was looking for capacitors to activate the Medium in the nearby village. Have you seen any?
		{subtopic("capacitor")}
		{goto("shop_126")}
	{otherwise if Global.stat("riley/126/reward")}
		Thanks again! I'm heading out of here soon.
		{goto("shop_126")}
	{otherwise}
		Thanks for the help! I'm Riley, by the way.
		[You] Jackie. Say, is there a capacitor up here? I was told they were up in these towers.
		{subtopic("capacitor")}
		{goto("shop_126")}
{otherwise if talked > 0}
	Seriously, I could use some help with those deathgnats.
	[You] No worries, they're tricky to hit but I've got it.
	* [Hint: You can perform an uppercut by doing a lunge kick when crouched. It's well-suited to flying enemies.]
{otherwise}
	Ey! Jutre! Khur ine siram tes?
	[You] I'm sorry?
	You're a Vaeler? I'm stuck up here because of those two deathgnats! If you could help get rid of them, I'd really appreciate it! {remember("I found this strange girl stranded at the top of a scrap pile. How did she get here?")}
	> {skip()} (Accept Challenge) Uh, I'll see what I can do.
		[You]  Uh, I'll see what I can do.
		{main_speaker.track_enemies()}
	> {skip()} (Decline) I'm going to scope out the area first.
		[You] I'm going to scope out the area first.
		Uh... don't take too long please!
{exit()}

:capacitor
Uh, I don't really know about electronics. What do they look like?
[You] They're these cylinders of white metal, about as long as my forearm and twice as thick.
Oh, they're Hasyr? Have you checked the tower nearby?
[You] I imagine it'd be a likely place.
I'll just say: if there's anything plugged in, you really shouldn't remove it.
[You] Oh, why not?
+{riley/capacitor}
{Global.task_note("activate_the_medium", "Riley says the nearby structures could have capacitors, but I shouldn't remove them if they're plugged in.")}
The Hasyr structures have these active repair systems that keep them working. If you turn stuff off, they could collapse.
[You] ...
{back()}
{exit()}

:shop_126
> Say, could I buy some supplies off you?
	Sure, I have some spare gear.
	{shop()}
	{exiting()} Okay, see you around.
	{exit()}
> Until we meet again, Riley.
	See you!
	{exit()}
{exit()}

:_entry111
{main_speaker.saved}
	{Global.stat("riley/111/reward")}
		Thanks again.
		[You] Do you need any help getting down?
		{Global.stat("capacitor_113") and !Global.stat("riley/apology")}
			No.
		{otherwise}
			Thanks, but no. The deathgnats are really the only specters that worry me. I can just jump around the others, but those bullets are too much for me!
	{otherwise if !Global.stat("capacitor_113") or Global.stat("riley/apology")}
		Phew! You really saved me there, Jackie!
		{talked == 0} I was pinned down before you took out those deathgnats!
		{Global.add_stat("riley/111/reward")} {Global.add_item("bug", 31)} {Global.add_item("gem", 75)} Here's some of the loot I got before it hit the fan. I can take it from here.
		> Say, could I buy some supplies off you?
			Sure, I have some spare gear.
			{shop()}
			{exiting()} Okay, see you around.
			{exit()}
		> Okay, see you.
			{exit()}
	{otherwise}
		... Thanks, Jackie.
		{talked == 0} I was pinned down before you took out those deathgnats.
		> No problem.
			{Global.add_stat("riley/111/reward")} {Global.add_item("bug", 17)} {Global.add_item("gem", 45)} Here's some of the loot I got. I'll see myself down.
			{exit()}
		> I wanted to apologize for knocking down the tower...
			{subtopic("apology")}
			{Global.add_stat("riley/111/reward")} {Global.add_item("bug", 31)} {Global.add_item("gem", 75)} Oh, and here's some of the loot I got. I can take it from here.
			See you around, Jackie.
			{exit()}
{otherwise}
	{talked > 0}
		[You] Are you good?
		Good. How's the deathgnat situation?
		[You] In progress.
	{otherwise if !Global.stat("capacitor_113") or Global.stat("riley/apology")}
		Hey again, Jackie!
		[You] Hey. Are you okay?
		Uh, been better. I tried to get some salvage while the specters were dormant, but that went about as well as it looks.
		There's four deathgnats flying around, if you could take them out, I can get out of here.
		> Sure, I'll give it a shot.
			Please do!
			{main_speaker.track_enemies()}
		> I'll scope out the area and be back soon, alright?
			Alright...
	{otherwise}
		[You] Riley, are you okay?
		I've been better. There's four deathgnats keeping me stuck here.
		> I can take them out if you need help.
			... Yeah, honestly. I can't get them on my own.
			[You] Okay, stay here and I'll get them.
			{main_speaker.track_enemies()}
		> I'll be back later.
			...
{exit()}

:_entry113

{!Global.stat("riley/apology") and Global.stat("capacitor_113")}
	?{riley/capacitor}
		{Global.add_stat("riley/113/argue") == 1}
			Oh my God! I told you not to take that capacitor! {remember("Riley's angry at me for taking the capacitor from the tower. Honestly I think she's overreacting.")}
			[You] How was I supposed to know the whole tower would explode?
			Because I told you it would!
			[You] No you didn't! You said it might collapse! Not that the whole thing would pop like a balloon the second I unplugged the capacitor!
			What, would it falling on that village a week from now be fine?
			[You] Oh, whatever. It didn't fall on the village, so I don't see the issue.
			It's blocking the whole valley! There's specters crawling everywhere! And what happens when it shifts again? It could crush the wall!
			[You] I don't know! They move or something!
			Ugh.
		{otherwise}
			I'm not in a talky mood right now. We'll probably see each other again. But seriously, if someone tells you something is a bad idea, listen to them.
	{otherwise}
		{Global.add_stat("riley/113/argue") == 1}
			{talked == 0}
				Ine kes?! Nurane kuloron ine?
				[You] W-what?
				What? A Vaeler? Nevermind, I'm asking about what you did to collapse the tower!
			{otherwise}
				What did you do?!
			[You] What do you mean?
			The entire tower collapsed! Did you shut down the power or something?!
			?{armstrong/asked_tower}
				[You] I... uh, assumed it would take longer.
				You KNEW this would happen?
			{otherwise}
				{!task_exists("activate_the_medium")}
					[You] I just took this curious thing! I didn't know it was important!
				{otherwise}
					[You] ... I don't see how that would have caused the tower to fall apart.
				Vaelers...
				These buildings are ancient. They have some sort of self-repair system. If you break the electrical system, they fall apart. Did you seriously think nothing would happen if you took it out?
			You could have crushed that entire village in the valley!
			[You] Well, it didn't fall on the village, so I don't see the issue.
			It's blocking half of the valley! And what happens when it shifts again? It could crush the wall, or block the whole path.
			[You] I don't know! They move or something!
			Ugh.
		{otherwise}
			I'm not in the mood to talk with you right now.
	{exit()}

?{riley/apology} {!Global.stat("riley/113/apologize")}
	+{riley/113/apologize}
	Well, it's not the end of the world. The tower was damaged when it fell, and Armstrong thinks we can bust it open and tear down the whole thing.
	[You] Really? Wow!
	Yeah, we might end up turning a pretty big profit on the scrap, so long as there aren't too many specters inside. At the very least we can get it moved out of the valley bit by bit.
	Did you need anything?
	+{riley/113/talked}

// Never saved Riley up at the thing
{otherwise if !Global.stat("riley/113/talked") and !Global.stat("riley/126/reward")}
	// But they did talk to her
	?{riley/126/talked}
		+{riley/not_saved}
		[You] Oh, I thought you were stranded on top of the scrap tower over there?
		I managed to get down on my own. I never caught your name by the by.
		[You] Oh, I'm Jackie. Sorry. I meant to help, but those creatures are rather difficult to hit.
		Riley. Don't worry about it. I'll try again later. I couldn't get a good look if there was a capacitor up there.
	{otherwise}
		{format("italic")}N'seit mi!
		> {skip()} N'seit mi.
			+{language/nseit_mi}
			[You] nset me.
			// Your accent is interesting. Where are you from?
			{format("italic")} Yuukirha inere uko ghimres. Pol tu ine?
			[You] Uh... I don't know.
		> Uh, hello.
		Whoa, a Vaeler! I didn't know you guys ever came out here! Seems too sunny for you.
		[You] Uh, I manage.
		I'm Riley, by the way.
		[You] Jackie.... So what is this place?
		+{hasyr}It's a tower built by the Hasyr.  There's all sorts of structures like it. They're MILLIONS of years old!
		[You] Really? That stretches belief.
		Yeah! They've been carbon dated and everything. The metal goes back to like two million years ago.
		{Global.task_exists("activate_the_medium")}
			[You] So I've been looking for ancient capacitors. Would there be one in the tower.
			Oh. Uh. Yeah, most of the structures have capacitors. But you shouldn't take them.
			[You] Why not?
			+{riley/capacitor}
			{Global.note_task("activate_the_medium", "Riley says there's a capacitor at the top of the white tower, but that I shouldn't take it or the tower would collapse. I'm not sure I see what the harm of that is if it's been abandoned for centuries.")}
			They're so old that they're on the brink of collapse. They have active, like, self-repair systems that keep them running. If the power gets interrupted, like by taking out capacitors, they can completely fall apart.
			[You] ...
		{otherwise}
			[You] What's inside them? Anything interesting?
			Well, yeah. They can be pretty cool, as long as you can fight off specters.
	+{riley/113/talked}
	{exit()}

{otherwise if Global.add_stat("riley/113/talked") == 1}
	[You] Hi Riley.
	Hi again, uh...
	[You] Jackie.
	Right! Hi Jackie. Thanks for the help back with those deathgnats.
	[You] So this is the tower you were talking about? With the capacitor?
	Right. You shouldn't take it.
	[You] ... Well, where else would I get a capacitor?
	+{riley/capacitor}
	{Global.note_task("activate_the_medium", "Riley says there's a capacitor at the top of the white tower, but that I shouldn't take it or the tower would collapse. I'm not sure I see what the harm of that is if it's been abandoned for centuries.")}
	I'm not sure. But seriously. These things are so old they're on the brink of collapse. They have active, like, self-repair systems that keep them running. If the power gets interrupted, like by taking out capacitors, they can completely fall apart.
	[You] ...
{otherwise if !Global.stat("capacitor_113") and !Global.stat("riley/113/days_collapse")}
	+{riley/capacitor}
	[You] Do you really think this thing would collapse if I took the capacitor?
	+{riley/113/days_collapse} Yeah. They're not as strong as they look. But they have these automatic repair machines keeping them running. Without power, it might only last a few days.
	[You] ...
{otherwise}
	Hey again Jackie. Did you need anything?
> {Global.stat("capacitor_113") and !Global.stat("riley/apology")} I wanted to apologize...
	{subtopic("apology")}
	{exit()}
> Say, could I buy some supplies off you?
	Sure, I have some spare gear.
	{shop()}
	{exiting()} Okay, see you around.
	{exit()}
> Okay, see you.
	{exit()}
{exit()}

:apology
[You] I should have listened to you. I could have found some other way to power up the Medium. I'm sorry for nearly destroying the village.
Well... It's not like anyone got hurt. And you've helped me out plenty.
{Global.add_stat("riley/apology")} I'm not one to hold a grudge, so let's just consider it a lesson learned.
[You] Thanks, Riley. {remember("I can see why Riley was as angry as she was about the tower. I'm glad she forgives me, at least.")}
{back()}
{exit()}

:_exit
[You] Gotta go, bye!
{exit()}

:_coat
{traded_coats()} [You] Say, could we trade coats again?
{otherwise} [You] That's a very lovely coat. Would you be interested in swapping for mine?

{main_speaker.needs_saving()} Uh, a little busy right now? A little pinned-down-by-gunfire right now?
	[You] Okay, I'll get the deathgnats first.
{otherwise if Global.stat("capacitor_113") and !Global.stat("riley/apology")} Uh, maybe some other time.
{otherwise if traded_coats()} Again? Did you regret the trade?
	[You] Well, I'm just quite picky with coats.
	I suppose we can.
	{swap_coats()} [You] Thanks!
{otherwise} You want to trade coats? Like, my coat for yours?
	[You] Yes.
	You're so odd. But sure, let's trade.
	{swap_coats()} [You] Thanks!
{exit()}